WHITESPACE = _{ " " }

vulgar_fraction = {
  "\u{00BC}" |
  "\u{00BD}" |
  "\u{00BE}" |
  "\u{2150}" |
  "\u{2151}" |
  "\u{2152}" |
  "\u{2153}" |
  "\u{2154}" |
  "\u{2155}" |
  "\u{2156}" |
  "\u{2157}" |
  "\u{2158}" |
  "\u{2159}" |
  "\u{215A}" |
  "\u{215B}" |
  "\u{215C}" |
  "\u{215D}" |
  "\u{215E}"
}

compound_vulgar_fraction = _{ integer? ~ vulgar_fraction }

simple_fraction = { integer ~ "/" ~ integer }

// ideally this would be _$, but silent + atomic is currently not possible
integer_and_whitespace = ${ integer ~ WHITESPACE }

compound_simple_fraction = _{ integer_and_whitespace? ~ simple_fraction }

fraction = { compound_simple_fraction | compound_vulgar_fraction }

integer = @{ ASCII_DIGIT+ }

float = @{ integer? ~ ("." | ",") ~ integer }

word_digit = { "an" | "a" | "one" | "two" | "three" }

constant = { fraction | float | integer | word_digit }

range = { constant ~ ("-" | "~") ~ constant }

preposition = { "of" }

unit = { "kg" | "lb" | "cups" | "cup" }

amount = { range | constant }

container_size_inner = _{ amount ~ "-"? ~ unit } // e.g. five-ounce and 5 ounce
container_size = {
  "(" ~ container_size_inner ~ ")" |
  "[" ~ container_size_inner ~ "]" |
  "{" ~ container_size_inner ~ "}"
}

unit_and_preposition = _{ unit ~ preposition? }

// when amount is given, unit becomes optional
quantity = _{ (amount ~ unit_and_preposition? | unit_and_preposition) ~ container_size? }

ingredient = { ANY* }

ingredient_alt = { (!(quantity ~ EOI) ~ ANY)* }

reverse_format = _{ ingredient_alt ~ quantity }

standard_format = _{ quantity ~ ingredient }

ingredient_and_amount_and_unit = _{ SOI ~ (standard_format | reverse_format) ~ EOI }
