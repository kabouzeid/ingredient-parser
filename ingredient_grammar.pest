W = _{ " " }

vulgar_fraction = {
  "\u{00BC}" |
  "\u{00BD}" |
  "\u{00BE}" |
  "\u{2150}" |
  "\u{2151}" |
  "\u{2152}" |
  "\u{2153}" |
  "\u{2154}" |
  "\u{2155}" |
  "\u{2156}" |
  "\u{2157}" |
  "\u{2158}" |
  "\u{2159}" |
  "\u{215A}" |
  "\u{215B}" |
  "\u{215C}" |
  "\u{215D}" |
  "\u{215E}"
}

compound_vulgar_fraction = _{ integer? ~ W* ~ vulgar_fraction }

simple_fraction = { integer ~ W* ~ "/" ~ W* ~ integer }

compound_simple_fraction = _{ (!(simple_fraction) ~ integer ~ W+)? ~ simple_fraction }

fraction = { compound_simple_fraction | compound_vulgar_fraction }

integer = @{ ASCII_DIGIT+ }

decimal_separator = _{ "." | "," }

float = @{ integer? ~ decimal_separator ~ integer }

constant = { fraction | float | integer | word_digit ~ &(W+ | EOI) }

range_separator = _{ "-" | "~" }

range = { constant ~ W* ~ range_separator ~ W* ~ constant }

amount = { range | constant }

unit_and_preposition = _{ unit ~ (W+ ~ preposition)? }

quantity = _{
  amount ~ W* ~ unit_and_preposition ~ &(W+ | EOI) |
  amount ~ &(W+ | EOI) |
  unit_and_preposition ~ &(W+ | EOI)
}

ingredient_lookahead = { (!(W+ ~ quantity ~ W* ~ EOI) ~ ANY)* }

ingredient = { (!(W* ~ EOI) ~ ANY)* }

// "flour 1 kg"
reverse_format = _{ ingredient_lookahead ~ W+ ~ quantity }

// "1 kg", "1 kg flour", "8 tomatoes"
standard_format = _{ quantity ~ W* ~ ingredient }

// note that this is *not* the same as having the choice inside of SOI and EOI
// eg like this: SOI ~ W* ~ (a | b | c | d) ~ W* ~ EOI
main = _{
  SOI ~ W* ~ standard_format ~ W* ~ EOI |
  SOI ~ W* ~ reverse_format ~ W* ~ EOI |
  SOI ~ W* ~ ingredient ~ W* ~ EOI // fallback, matches everything
}

// WILL BE AUTOGENERATED
// word_digit = { ^"an" | ^"a" | ^"one" | ^"two" | ^"three" ... }
// unit = { ^"kg" | ^"lb" | ^"cups" | ^"cup" ... }
// preposition = { ^"of" }
